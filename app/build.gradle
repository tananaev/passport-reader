plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 36
    ndkVersion '23.1.7779620'
    defaultConfig {
        applicationId 'com.tananaev.passportreader'
        minSdkVersion 23
        targetSdkVersion 36
        versionCode 21
        versionName '3.2'
        multiDexEnabled = true
    }
    namespace 'com.tananaev.passportreader'

    buildFeatures {
        flavorDimensions = ['default']
    }
    productFlavors {
        regular {
            isDefault = true
            ext.enableCrashlytics = false
        }
        google
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'com.wdullaer:materialdatetimepicker:3.5.2'
    implementation 'org.jmrtd:jmrtd:0.7.18'
    implementation 'net.sf.scuba:scuba-sc-android:0.0.18'
    implementation 'com.madgag.spongycastle:prov:1.54.0.0'
    implementation 'io.github.CshtZrgk:jp2-android:1.0.0'
    implementation 'com.github.mhshams:jnbis:1.1.0'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.65' // do not update
    implementation 'commons-io:commons-io:2.11.0'
    googleImplementation platform('com.google.firebase:firebase-bom:33.16.0')
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    googleImplementation 'com.google.firebase:firebase-crashlytics'
    googleImplementation 'com.google.android.gms:play-services-ads:24.4.0'
    googleImplementation 'com.google.android.play:review-ktx:2.0.2'
}

tasks.register('copyFirebaseConfig', Copy) {
    from '../../environment/firebase'
    into '.'
    include 'passport-reader.json'
    rename('passport-reader.json', 'google-services.json')
}
afterEvaluate {
    tasks.matching { it.name.contains('Google') }.configureEach { task ->
        if (task.name.contains('Regular')) {
            task.enabled false
        } else {
            task.dependsOn copyFirebaseConfig
        }
    }
}
